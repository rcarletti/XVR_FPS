
#define SWAT_START_WALK 16
#define SWAT_END_WALK   48

#include <weapon.s3d>

SET MAX_HEALTH = 100.0;

class player
{
	/*graphics and sounds*/
	var lowerMesh;
	var upperMesh;
	var railgunMesh;
	var lowerObj;
	var upperObj;
	var weaponObj;
	var rootObj;
	var billboardFire;
	var fireObj;
	var sfxStep;
	var sfxShoot;
	var sfxHit;
	var sfxJam;
	var currentFrame;	
	
	/*movement*/
	
	var position;
	var direction;
	var angleY;
	var angleX;
	var speed;
	var angSpeedX;
	var angSpeedY;
	var verticalSpeed;
	var toward;
	var speedMultiplier;
	var isWalking;
	var upperRotation;
	
	/*attributes */
	var id;
	var color;
	var score;
	var hit;
	var weapons;
	var currWeapon;
	var bullets;
	var bulletNum;
	var magazines;
	var reloadTimer;
	var isReloading;
	var level;
	var isFalling;
	var health;


	/*functions */
	
	init(id,col,pos, matrix);
	update();
	draw();
	readKeyboard();
	swapWeapon(id);
	spawn();
};

//********************************************************************
//INIT PLAYER
//********************************************************************

function player :: init(id, col, pos, level){
	id = id;
	color = col;
	position = pos;
	angleY = 0;
	angleX = 0;
	speed = 0.03;
	angSpeedX = 0.1;
	angSpeedY = 0.1;
	verticalSpeed = 0.0;
	currentFrame = SWAT_START_WALK;
	hit = false;
	score = 0;
	toward = 0;
	speedMultiplier = 1;
	isWalking = false;
	this.level = level;
	weapons = Array(2);
	currWeapon = 0;
	bullets = array(0);
	bulletNum = 0;
	magazines = array(2);
	reloadTimer = 0;
	isReloading = false;
	isFalling = false;
	direction = [0,0,0];
	health = 100.0;

	//create player image
	rootObj  = CVmObj();
	
	lowerMesh = CVmNewMesh("swat_lower.aam");	
	upperMesh = CVmNewMesh("swat_upper.aam");
	railgunMesh = CVmNewMesh("railgun.aam");
	lowerMesh.Scale(0.045);
	upperMesh.Scale(0.045);
	railgunMesh.Scale(0.045);
	
	lowerObj  = CVmObj(lowerMesh);
	upperObj  = CVmObj(upperMesh);
	weaponObj = CVmObj();
	
	rootObj.AddChild(lowerObj);
	rootObj.AddChild(upperObj);
	rootObj.AddChild(weaponObj);
	
	//applica il colore a tutte le parti del giocatore
	rootObj.ModulateMaterials(color);
	
	billboardFire = CVmBillboard("flame43.avi");
	fireObj = CVmObj();
	fireObj.LinkToBillboard(billboardFire);
	
	rootObj.SetPosition(position);
	fireObj.SetPosition(position);
	upperObj.SetPivotPoint([0,1.0,0]); 
	weaponObj.SetPivotPoint([0,1.0,0]);
	
	sfxStep = CVmVRAWav("step.wav",true);
	sfxShoot = CVmVRAWav("shoot.wav",true);
	sfxHit = CVmVRAWav("hit.wav",true);
	sfxJam = CVmVrAWav("jam.wav",true);
	
	
	weapons[0] = weapon("railgun.aam");
	weapons[1] = weapon("rocketl.aam");
	hit = false;
	swapWeapon(0);
	magazines[0] = 2;
	magazines[1] = 2;
}

//********************************************************************
//DRAW
//********************************************************************

function player::draw()
{
	//railgunObj.DrawBoundingBox(VR_WIREFRAME);
	rootObj.Draw(VR_FRAMENUMBER, currentFrame);
	
	foreach (var b in bullets) {
		b.draw();
	}
	
	if (hit)
	{
		glEnable(GL_BLEND);
		glBlendFunc(GL_ONE, GL_ONE_MINUS_SRC_COLOR);
		fireObj.Draw();
		glBlendFunc(GL_ONE, GL_ONE_MINUS_SRC_ALPHA);
		glDisable(GL_BLEND);
	}
	

}

//********************************************************************
//PLAYER UPDATE
//********************************************************************

function player :: update(){
	
	readKeyboard();
	
	//mouse coords
	var mouseDelta = [Mouse.DeltaX, Mouse.DeltaY];
	angleY -= angSpeedY * mouseDelta.x;
	angleX -= angSpeedX * mouseDelta.y;
	
	if (angleX >= 50) {
		angleX = 50;
	}
	
	if (angleX <= -40) {
		angleX = -40;
	}
	
	direction = [sin(angleY * GRAD_TO_RAD), sin(angleX * GRAD_TO_RAD), cos(angleY * GRAD_TO_RAD)];	
	
	
	//set positions
	rootObj.SetPosition(position);	
	rootObj.SetRotation(angleY,0,1,0);
	upperObj.SetRotation(-angleX,1,0,0);
	weaponObj.SetRotation(-angleX, 1,0,0);
	fireObj.SetPosition(position + [0,1,0]);
	sfxStep.SetPosition(position);
	sfxHit.SetPosition(position);
	sfxShoot.SetPosition(position);
	sfxJam.SetPosition(position);
	sfxStep.SetListenerPosition(CameraGetPosition());
	sfxStep.SetListenerOrientation(-CameraGetDirection());
	
	/*frame update */
	
	if (isWalking) {
		var deltaFrame = 0.25 * speedMultiplier;
		currentFrame += deltaFrame;
		
		if (currentFrame >= SWAT_END_WALK) {
			currentFrame = SWAT_START_WALK;
			sfxStep.Play();
		}
		else if (currentFrame == ((SWAT_END_WALK - SWAT_START_WALK) / 2) + SWAT_START_WALK) {
			sfxStep.Play();
		}
		
	}
	
	/*bullets update */
	
	for (var i = 0; i < bulletNum; i++) {
		bullets[i].update(level);
		if (bullets[i].life == 0) {
			adel(bullets, i);
			bulletNum--;
			i--;
		}
	}
	
	
	/*reload update */
	
	if(GetTime() - reloadTimer  > weapons[currWeapon].reloadTime) {
		isReloading = false;
	}
}

//********************************************************************
//READ INPUT FROM KEYBOARD 
//********************************************************************

function player :: readKeyboard(){
	
	var strafeDirection = [0, 0, 0];
	var walkDirection = [0, 0, 0];
	isWalking = false;
	speedMultiplier = 1;		
	

	/*go right */
	if (Keypressed("D")) {			
		isWalking = true;
		strafeDirection = VectorRotate(-90, [0,1,0], direction.x_z);
	}
	
	/*go left */
	if (Keypressed("A")) {			
		isWalking = true;
		strafeDirection = VectorRotate(90, [0,1,0], direction.x_z);
	}
	
	/*run */
	if (Keypressed(VK_SHIFT) && !isFalling) {	
		isWalking = true;
		speedMultiplier = 2;		
	}
	
	/* go forward */
	if (Keypressed("W")) {			
		isWalking = true;
		walkDirection = direction.x_z;
	}
	
	/*go backwards */
	if (Keypressed("S")) {			
		isWalking = true;
		walkDirection = - direction.x_z;
	}

	
	if (Keypressed(VK_SPACE) && !isFalling) {			
		isFalling = true;
		verticalSpeed = 0.12;
	}
	
	/*shoot */
	static var hasShot = false;
	if (Mouse.ButtonL && weapons[currWeapon].bullets > 0 && !hasShot && !isReloading) { 
		hasShot = true;
		bulletNum++;
		weapons[currWeapon].bullets--;
		var tmpBullet = bullet();
		tmpBullet.init([0, 0, 0], weapons[currWeapon].bulletSpeed);
		aadd(bullets, tmpBullet);
		var bbox = railgunMesh.SubsetGetBoundingBox(0);
		var tmpPos = position + VectorRotate(angleY, [0, 1, 0], [(bbox[0] + bbox[3]) / 2, (bbox[1] + bbox[4]) / 2, bbox[5]]) - [0, 0.3, 0];
		var shot = tmpBullet.shoot(tmpPos,direction);
		if (shot == 1) {
			sfxShoot.Play();
		}
		else {
			sfxJam.Play();
		}
	}
	else if(!Mouse.ButtonL)
		hasShot = false;
	
	/*swap weapons */
	static var isSwitched = false;
	if(Keypressed("1") && !isSwitched){
		if(currWeapon == 1){
			isSwitched = true;
			swapWeapon(0);
		}
		else{
			swapWeapon(1);
			isSwitched = true;
		}
		
	}
	else if(!Keypressed("1")){
		isSwitched = false;
	}
	
	/*reload */
	if (Keypressed("R") && !isReloading) {	
		if (magazines[currWeapon] > 0) {
			weapons[currWeapon].bullets = weapons[currWeapon].magazineSize;
			magazines[currWeapon]--;
			reloadTimer = getTime();
			isReloading = true;
		}	
	}
	
	
	/*update position */
	var oldPosition = position;
	position += ((strafeDirection + walkDirection) * speed * speedMultiplier);

	/*check collision */
	var h = level.LevelMatrix[int(position.z)][int(position.x)];	
	if(level.isColliding(position, 0.8)){
		position = oldposition;
	}

	/* update falls and jumps */
	verticalSpeed -= 0.006;
	position.y += verticalSpeed;

	if (position.y > h){
		isFalling = true;
	} else if (position.y <= h) {
		verticalSpeed = 0;
		position.y = h;
		isFalling = false;
	}

	/*get ammos */
	static var isPickingAmmos = false;
	var name = level.isWeaponPresent(position.x, position.z);
	if (name != "" && !isPickingAmmos) {
		isPickingAmmos = true;
		level.pickUpAmmos(position.x, position.z);
		if(name == "railgun.aam" && magazines[0] < 5) {
			magazines[0]++;
		}
		else if ( magazines[1] < 5 && name == "rocketl.aam"){
			magazines[1]++;
		}
	}
	else if(name == "") {
		isPickingAmmos = false;
	}
	
}

//********************************************************************
//SWAP WEAPONS
//********************************************************************

function player :: swapWeapon(weaponID) {
	weaponObj.RemoveChild(weapons[currWeapon].weaponObj);
	currWeapon = weaponID;
	weaponObj.AddChild(weapons[currWeapon].weaponObj);
}

//********************************************************************
//SPAWN PLAYER
//********************************************************************

function player :: spawn() {
	var pos, posx, posz;
	/*spawn outside walls */
	do {		
		posx = rand(level.columns);
		posz = rand(level.rows);
		pos = [posx, 0, posz];
	}
	while(level.isColliding(pos, 0.8));
	health = MAX_HEALTH;

}