#define MAX_LIFE   3

class bullet
{
	var id;
	var position;
	var prevPosition;
	var direction;
	var speed;
	var bulletMesh;
	var bulletObj;
	var life;
	var isFlying;
	var color;
	var collisionSegment;
	var radius;
	
	init(id, col, vel, radius);
	shoot(pos,dir);
	update(level, deltaFrame);
	draw();
};



function bullet::init(id, col, speed, radius)
{
	bulletMesh = CVMNewMesh(VRP_SPHERE);
	bulletMesh.Scale(0.1);
	bulletObj = CVmObj(bulletMesh);
	this.id = id;
	this.speed = speed;
	life = 0;
	this.radius = radius;
	prevPosition = position = [0.0, 0.0, 0.0];
	direction = [0.0, 0.0, 0.0];
	color = col;
	bulletMesh.ModulateMaterials(color);
	collisionSegment = Array(2);
}

function bullet::shoot(pos, dir)
{
	prevPosition = position = pos;
	direction = dir;
	life = MAX_LIFE;
	return 1;
}

function bullet::draw()
{
	bulletObj.Draw();
}

function bullet::update(level, deltaFrame)
{
	prevPosition = position;
	position += direction * speed * deltaFrame;

	collisionSegment[0] = position + direction - [0, 0.1, 0];
	collisionSegment[1] = position + direction + [0, 0.1, 0];

	if(level.map.IsColliding(collisionSegment[0],collisionSegment[1])){
		life = 0;
	}
	else{
		bulletObj.SetPosition(position);
		life-= deltaFrame;	
	}

}

